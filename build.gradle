/*
 * Copyright 2013 Noam Y. Tenne
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
        jcenter()
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath(group: 'org.jfrog.buildinfo', name: 'build-info-extractor-gradle', version: '3.0.1')
        classpath 'com.gradle.publish:plugin-publish-plugin:0.9.3'
    }
}

apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.artifactory'
apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'project-reports'
apply plugin: 'com.gradle.plugin-publish'

group = 'org._10ne.gradle'

repositories {
    jcenter()
}

dependencies {
    compile gradleApi()
    compile 'org.codehaus.groovy:groovy-all:2.3.9'
    compile('org.codehaus.groovy.modules.http-builder:http-builder:0.7.1') {
        exclude group: 'org.codehaus.groovy', module: 'groovy-all'
        exclude group: 'org.codehaus.groovy', module: 'groovy'
    }
    testCompile ('org.spockframework:spock-core:0.7-groovy-2.0') {
        exclude group: 'org.codehaus.groovy', module: 'groovy-all'
    }
    testCompile 'cglib:cglib-nodep:2.2.2'
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

configure(install.repositories.mavenInstaller) {
    pom.project {
        name('rest-gradle-plugin')
        description('A Gradle plugin that provides a task infrastructure to perform REST requests.')
        url('https://github.com/noamt/rest-gradle-plugin')
        inceptionYear '2013'
        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution 'repo'
            }
        }
        scm {
            url "https://github.com/noamt/rest-gradle-plugin.git"
        }
        developers {
            developer {
                id 'noamt'
                name 'Noam Y. Tenne'
                roles {
                    role 'Developer'
                }
            }
        }
    }
}

artifactory {
    contextUrl = 'https://oss.jfrog.org'
    resolve {
        repoKey = 'remote-repos'
    }
    publish {
        repoKey = 'oss-release-local'
        defaults {
            publishArtifacts = true
        }
    }
}

publishing {
    publications {
        mainPub(MavenPublication) {
            from components.java

            artifact sourceJar {
                classifier 'sources'
            }
        }
    }
}

artifactoryPublish {
    publications(publishing.publications.mainPub)
}

pluginBundle {
    website = 'http://www.10ne.org/'
    vcsUrl = 'https://github.com/noamt/rest-gradle-plugin.git'
    description = 'A Gradle plugin that provides a task infrastructure to perform REST requests.'
    
    plugins {
        restPlugin {
            id = 'org.10ne.rest'
            displayName = 'REST Gradle Plugin'
            tags = ['REST', 'API']
            version = '0.4.1'
        }
    }

    mavenCoordinates {
        groupId = "org._10ne.gradle"
        artifactId = "rest-gradle-plugin"
        version = '0.4.1'
    }
}

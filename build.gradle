/*
 * Copyright 2013 Noam Y. Tenne
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'maven'

group = 'org._10ne.gradle'
version = '0.2-SNAPSHOT'

buildscript {
    apply from: 'gradle/Bintray.gradle'
}

repositories {
    bintray.jcenter()
}

dependencies {
    compile gradleApi()
    groovy 'org.codehaus.groovy:groovy-all:2.1.2'
    compile('org.codehaus.groovy.modules.http-builder:http-builder:0.6') {
        exclude group: 'org.codehaus.groovy', module: 'groovy-all'
        exclude group: 'org.codehaus.groovy', module: 'groovy'
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

task wrapper(type: Wrapper, description: 'Gradle Wrapper task') {
    gradleVersion = '1.5'
}

configure(install.repositories.mavenInstaller) {
    pom.project {
        name('rest-gradle-plugin')
        description('A Gradle plugin that provides a task infrastructure to perform REST requests.')
        url('https://github.com/noamt/rest-gradle-plugin')
        inceptionYear '2013'
        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution 'repo'
            }
        }
        scm {
            url "https://github.com/noamt/rest-gradle-plugin.git"
        }
        developers {
            developer {
                id 'noamt'
                name 'Noam Y. Tenne'
                roles {
                    role 'Developer'
                }
            }
        }
    }
}